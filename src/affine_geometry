function dist(v::Vector{<:Real}, w::Vector{<:Real})
    return norm(v-w)
end

function sqdist(v::Vector{<:Real}, w::Vector{<:Real})
    return sum((v-w).^2)
end

"""
    signedangle3d(v::Vector{<:Real}, w::Vector{<:Real}, n::Vector{<:Real}; atol = 1e-12)

Calculate the signed angle of the right handed rotation from the vector v to w with regard to the plane normal vector n. Real values < atol are considered 0. 
"""
function signedangle3d(v::Vector{<:Real}, w::Vector{<:Real}, n::Vector{<:Real}; atol = 1e-12)
    # https://stackoverflow.com/questions/5188561/signed-angle-between-two-3d-vectors-with-same-origin-within-the-same-plane
    @assert length(v) == 3 "Inputs need to be real vectors of length 3."
    @assert length(w) == 3 "Inputs need to be real vectors of length 3."
    @assert abs(dot(v,n)) < atol "v and n are not perpendicular. abs(dot(v,n)) = $(abs(dot(v,n)))"
    @assert abs(dot(w,n)) < atol "w and n are not perpendicular. abs(dot(w,n)) = $(abs(dot(w,n)))"
    return atan(dot(cross(v,w), n), dot(v,w))
end


"""
    affinebasis_indices(s::Vector{<:Vector{<:Real}})

Find the indices of the entries in s that form an affine basis of the affine space spanned by the entries of s.
Real values < atol are considered 0.
"""
function affinebasis_indices(s::Vector{<:Vector{<:Real}}; atol::Real = 1e-8)
    d = length(s[1])
    @assert all([length(v) == d for v in s]) "Dimension mismatch in the set of affine points."
    sol = []
    for (i,v) in enumerate(s)
        A = hcat(s[sol]..., v)
        A = vcat(A, reshape(repeat([1], length(sol) + 1), (1, length(sol) + 1)))
        if length(colspace(A, tol = atol)) == length(sol) + 1
            append!(sol, i)
        end
        if length(sol) == d+1
            return sol
        end
    end

    return sol
end

"""
    affinebasis(s::Vector{<:Vector{<:Real}})

Find an affine basis of the affine space spanned by the points in s.
Real values < atol are considered zero.
"""
function affinebasis(s::Vector{<:Vector{<:Real}}; atol::Real = 1e-8)
    return s[affinebasis_indices(s, atol = atol)]
end

"""
    affinedim(s::Vector{<:Vector{<:Real}}; atol::Real = 1e-8)

Affine dimension of the affine space spanned by the entries of s.
Real values < atol are considered zero.
"""
function affinedim(s::Vector{<:Vector{<:Real}}; atol::Real = 1e-8)
    return length(affinebasis(s, atol = atol)) - 1
end


"""
    affinemap(preim::Vector{<:Vector{<:Real}}, im::Vector{<:Vector{<:Real}})

TBW
"""
function affinemap(preim::Vector{<:Vector{<:Real}}, im::Vector{<:Vector{<:Real}}; atol = 1e-8)
    @assert length(preim) == length(im) "Preimage ind image have to have the same length"

    d_pre = length(preim[1]) # dimension of underlying space of preimage
    @assert affinedim(preim) == d_pre "preim needs to contain an affine basis, but span of points has affine dimension $(affinedim(preim)) < $(d_pre)"
    d_im = length(im[1]) # dimension of underlying space of image
    @assert all([length(v) == d_pre for v in preim]) "Dimension of preimage vectors inconsistent."
    @assert all([length(v) == d_im for v in im]) "Dimension of image vectors inconsistent."

    basisind = affinebasis_indices(preim)
    preimbasis = preim[basisind]
    imbasis = im[basisind]

    
    A = hcat(preimbasis...)
    A = vcat(A, reshape(repeat([1], d_pre + 1), (1, d_pre + 1))) # embed preimage into higher dimensional space
    b = hcat(imbasis...)
    b = vcat(b, reshape(repeat([1], d_pre + 1), (1, d_pre + 1))) # embed image into higher dimensional space

    @assert length(colspace(A, tol = atol)) == d_pre + 1 "The preimage needs to consist of an affine basis."

    function aff(x::Vector{<:Real})
        M = b * inv(A)
        return (M * vcat(x, [1]))[1:(end-1)]
    end

    return aff
end

"""
    rigidmap(preim::Vector{<:Vector{<:Real}}, im::Vector{<:Vector{<:Real}})

TBW
"""
function rigidmap(preim::Vector{<:Vector{<:Real}}, im::Vector{<:Vector{<:Real}}; atol::Real = 1e-8)
    @assert length(preim) == length(im) "Preimage ind image have to have the same length"

    for i in 1:length(preim)
        for j in (i+1):length(preim)
            @assert abs(dist(preim[i], preim[j]) - dist(im[i], im[j])) < atol "Distance between preimage and image points need needs to be identical, but the distance between the points $(i) and $(j) is $(dist(preim[i], preim[j])) in the perimage and $(dist(im[i], im[j])) in the image."
        end
    end

    return affinemap(preim, im, atol=atol)
end

